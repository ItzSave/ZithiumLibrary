plugins {
    id 'java'
    id 'maven-publish'
    id 'com.gradleup.shadow' version '8.3.5'
}

group = 'net.zithium.library'
version = '2.1.11'

repositories {
    mavenCentral()
    maven { url = 'https://repo.papermc.io/repository/maven-public/' }
    maven { url = 'https://libraries.minecraft.net/' }
    maven { url = 'https://repo.extendedclip.com/releases/' }
}

dependencies {
    compileOnly 'io.papermc.paper:paper-api:1.20.4-R0.1-SNAPSHOT'
    compileOnly 'com.mojang:authlib:1.5.21'

    // For MiniMessage text formatting.
    implementation "net.kyori:adventure-text-minimessage:4.23.0"
    implementation "net.kyori:adventure-text-serializer-legacy:4.23.0"

    // XMaterial, XSound, ReflectionUtils, ActionBar
    implementation "com.github.cryptomorin:XSeries:13.2.0"

    // PlaceholderAPI does not need to be shaded as it's optional.
    compileOnly 'me.clip:placeholderapi:2.11.6'
}

java {
    toolchain.languageVersion.set(JavaLanguageVersion.of(17))
}

build {
    dependsOn(shadowJar)
}

shadowJar {
    dependencies {
        include(dependency("net.kyori:adventure-text-minimessage"))
        include(dependency("net.kyori:adventure-text-serializer-legacy"))
    }



    relocate "net.kyori.adventure", "net.zithium.library.shaded.adventure"
    relocate "com.cryptomorin.xseries", "net.zithium.library.shaded.xseries"
    archiveFileName="ZithiumLibrary-${project.version}.jar"
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            groupId = group
            artifactId = project.name
            version = project.version
        }
    }
    repositories {
        maven {
            url 'https://jitpack.io'
        }
    }
}
